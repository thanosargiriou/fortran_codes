      PROGRAM SHADOW
C******************************************************************
C CALCULATES THE SHADING COEFFICIENT OF A ROTATING SHADING DEVICE
C CREATED IN THE FRAME OF THE SOLAR HOUSE PROJECT
C "PAPAGEORGIOU GENERAL TEACHING HOSPITAL - THESSALONIKI"
C ALL PHYSICAL PARAMETERS MUST BE GIVEN IN SI UNITS
C
C ATHANASSIOS A. ARGIRIOU, START 18 SEPTEMBER 1994
C******************************************************************
      DIMENSION SPX(4),SPY(4),SPZ(4),PROJY(4),PROJZ(4)
C
C VARIOUS CONSTANTS
C
      PI=4.*ATAN(1.)
C
C MULTIPLY WITH THIS CONSTANT TO CONVERT AN ANGLE FROM DEG TO RAD
C DIVIDE BY THIS CONSTANT TO CONVERT AN ANGLE FROM RAD TO DEG
      R=PI/180.
C
C DEFINITION OF THE WALL DIMENSIONS
C
      CALL WALLDEF(XLW,XHW,WAZM,WLAT)
C
C DEFINITION OF THE SOLAR SHADING DEVICE
C
      WRITE(*,'(A)')' GIVE THE NUMBER OF THE POINTS DEFINING THE 
     &CONTOUR OF THE SHADING DEVICE'
      READ(*,*)NCONT
      DO I=1,NCONT	
      CALL SHADDEF(I,X,Y,Z)
      SPX(I)=X
      SPY(I)=Y
      SPZ(I)=Z
      ENDDO
C
C DEFINITION OF THE CALCULATION PERIOD
C
      CALL CALCPER(DAY,MONTH,SHOUR)
C
C CALCULATION OF THE SUN CO-ORDINATES AT THE MOMENT OF CALCULATIONS
C
      CALL SDEC(DAY,MONTH,DEC)
      CALL SANG(SHOUR,DAY,MONTH,WLAT,DEC,SALT,SAZM)
C
C CALCULATION OF THE SOLAR AZIMUTH AT THE CO-ORDINATE SYSTEM OF THE WALL
C
      SAZMW=SAZM+(PI-WAZM*R)
C
C CALCULATION OF THE PROJECTIONS OF THE POINTS DEFINING THE SHADING DEVICE 
C ON THE OYZ LEVEL
C
      DO I=1,NCONT
      CALL PROJECTION(SPX(I),SPY(I),SPZ(I),SAZMW,SALT,PROJY(I),PROJZ(I))
      WRITE(*,*)PROJY(I),PROJZ(I)
      ENDDO 
C
      END
C
      SUBROUTINE PROJECTION(X,Y,Z,SAZMW,SALT,PRY,PRZ)
C*********************************************************************
C INPUT DATA: X,Y,Z CO-ORDINATES OF A POINT
C             SOLAR AZIMUTH IN THE WALL CO-ORDINATE SYSTEM
C             SOLAR ALTITUDE        
C OUTPUT DATA: PRY = Y CO-ORDINATE OF THE PROJECTION
C              PRZ = Z CO-ORDINATE OF THE PROJECTION
C***********************************************************************
      PI=4.*ATAN(1.)
      PRY=Y-X*TAN(PI-SAZMW)
      PRZ=Z-SQRT(X*X+ABS(Y-PRY)**2)*TAN(SALT)
      RETURN
      END
C
      SUBROUTINE WALLDEF(XLW,XHW,WAZM,WLAT)
C******************************************************************
C DEFINITION OF THE WALL GEOMETRY
C XLW : WALL WIDTH
C XHW : WALL HEIGHT
C WAZM: AZIMUTH OF THE WALL (0 DEG NORTH, 180 DEG SOUTH CLOCKWISE)
C WLAT: LATITUDE OF THE PLACE WHERE THE WALL IS (IN DEG AND TENTHS OF DEG)
C*******************************************************************
      WRITE(*,'(A)')' GIVE THE WIDTH OF THE WALL '
      READ(*,*)XLW
      WRITE(*,'(A)')' GIVE THE HEIGHT OF THE WALL '
      READ(*,*)XHW
      WRITE(*,'(A)')' GIVE THE AZIMUTH OF THE WALL '
      READ(*,*)WAZM
      WRITE(*,'(A)')' GIVE THE LATITUDE OF THE PLACE '
      READ(*,*)WLAT
      RETURN
      END
C
      SUBROUTINE SHADDEF(I,X,Y,Z)
C********************************************************************
C     ASKS FOR THE POINTS DEFINING THE CONTOUR OF THE SHADING DEVICE
C     EACH POINT IS DEFINED BY THE COORDINATES X,Y,Z
C***********************************************************************
      WRITE(*,'(A)')' ENTER THE X, Y, Z CO-ORDINATES OF THE POINT NO'
      WRITE(*,*)I
      READ(*,*)X,Y,Z
      RETURN
      END
C
      SUBROUTINE CALCPER(DAY,MONTH,SHOUR)
C**********************************************************************
C TIME DATA INPUT FOR THE CALCULATIONS
C
C**********************************************************************
      WRITE(*,'(A)')' ENTER THE MONTH, DAY AND HOUR'
      READ(*,*)MONTH,DAY,SHOUR
      RETURN

      END
C
C*****************************************************
      SUBROUTINE SDEC(DAY,MONTH,DEC)
C*****************************************************
C THIS ROUTINE CALCULATES SOLAR DECLINATION (DEC)
C ATHANASSIOS ARGIRIOU 2 - 5 - 1990
C
      PI=4.*ATAN(1.)
C
C CALCULATION OF DAY NO
C
      GOTO(1,2,3,4,5,6,7,8,9,10,11,12)MONTH
 1    DA=DAY
      GOTO 13
 2    DA=DAY+31.
      GOTO 13
 3    DA=DAY+59.
      GOTO 13
 4    DA=DAY+90.
      GOTO 13
 5    DA=DAY+120.
      GOTO 13
 6    DA=DAY+151.
      GOTO 13
 7    DA=DAY+181.
      GOTO 13
 8    DA=DAY+212.
      GOTO 13
 9    DA=DAY+243.
      GOTO 13
 10   DA=DAY+273.
      GOTO 13
 11   DA=DAY+304.
      GOTO 13
 12   DA=DAY+334.
C CALCULATION OF DECLINATION
 13   DEC=23.44*SIN(2.*PI*(DA-81.)/365.)
      RETURN
      END
C
C***********************************************************
      SUBROUTINE SANG(SHOUR,DAY,MONTH,SLAT,DEC,SALT,SAZM)
C***********************************************************
C
C THIS ROUTINE CALCULATES THE SOLAR ALTITUDE (SALT) & SOLAR AZIMUTH
C (SAZM) USING THE ROUTINE SDEC TO CALCULATE THE DECLINATION. THE SOL
C HOUR (SHOUR) AND THE LATITUDE OF THE PLACE (SLAT) IS USED AS INPUT PAR
C AMETER
C
C ATHANASSIOS ARGIRIOU 2 - 5 - 1990
C
      PI=4.*ATAN(1.)
      R=PI/180.
C
C DECLINATION CALCULATION
C
      CALL SDEC(DAY,MONTH,DEC)
C
C ALTITUDE CALCULATION
C
      DEC1=DEC*R
      SLAT1=SLAT*R
      HR=HRANG(SHOUR)*R
      SALT=ASIN(SIN(DEC1)*SIN(SLAT1)+COS(DEC1)*COS(SLAT1)*COS(HR))
C
C AZIMUTH CALCULATION
      SAZM=ASIN(-SIN(HR)*COS(DEC1)/COS(SALT))
      IF (SHOUR.LE.12.)SAZM=PI-ABS(SAZM)
      IF (SHOUR.GT.12.)SAZM=PI+ABS(SAZM)
C
      RETURN
      END
C
      FUNCTION HRANG(X)
C********************************************************************
C THIS FUNCTION CALCULATES THE HOUR ANGLE CORRESPONDING TO THE SOLAR
C HOUR X.
C ATHANASSIOS ARGIRIOU, 2 - 5 - 1990
C********************************************************************
      IF(X.GE.12.)THEN
      HRANG=15.*(X-12.)
      ELSE
      HRANG=15.*(X+12.)
      ENDIF
      RETURN
      END
C