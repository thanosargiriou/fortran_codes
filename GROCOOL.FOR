      PROGRAM GROCOOL
C*******************************************************************
C GROUND COOLING BY HORIZONTAL BURIED PIPES
C ATHANASSIOS ARGIRIOU, 20 - 6 - 1990
C*******************************************************************
C
      EXTERNAL FINT
      COMMON BI,SA
      CHARACTER LOCAT*10,LOCAT1*10,INAM*6,INAM1*6
      DIMENSION RAD(3),XL(3),FV(2),TA(24),RH(24),TG(5)
C THERMOPHYSICAL PARAMETERS OF THE SOIL
      DATA DIFF/6.9E-07/,CSO/1.8/
C GEOMETRY OF THE PIPES
      DATA RAD(1),RAD(2),RAD(3)/.15,.2,.25/,
     $XL(1),XL(2),XL(3)/20.,40.,50./
C CONFIGURATION
      DATA DEP/1.5/,TISC/7200./,FV(1),FV(2)/3.,6./
C
      PI=4.*ATAN(1.)
C
C INPUT PARAMETERS
C
      WRITE(*,9200)
 9200 FORMAT(' ENTER GROUND TEMPERATURES FILE NAME')
      READ(*,9201)INAM1
 9201 FORMAT(A6)
      WRITE(*,9202)
 9202 FORMAT(' ENTER CLIMATE FILE NAME')
      READ(*,9203)INAM
 9203 FORMAT(A6)
      CALL FICHR(200,INAM1,'OLD',1)
      READ(200,8000)LOCAT1
 8000 FORMAT(A10)
      READ(200,*)(TG(J),J=1,5)
      CALL FICHR(200,INAM1,'OLD',2)
C
      CALL FICHR(100,INAM,'OLD',1)
      READ(100,*)NCIT
      DO 20 LW=1,NCIT
      READ(100,9000)LOCAT
 9000 FORMAT(A10)
      DO 20 LM=1,5
      READ(100,*)MONTH
      READ(100,*)(TA(J),J=1,24)
      READ(100,*)(RH(J),J=1,24)
C
C INITIALISATION
C
      DO 20 J=1,3
      DO 20 K=1,3
      DO 20 L=1,2
      Q=0.
      TIMER=0.
      SUR=PI*RAD(J)*RAD(J)
      WRITE(*,9001)LOCAT,MONTH
 9001 FORMAT(/,' Performance of buried pipes for ',A14,' Month ',I2,/)
      WRITE(*,9002)XL(K),RAD(J),FV(L)
 9002 FORMAT(' Pipe length = ',F5.2,' m,  Radius = ',F4.3,
     $' m, Air velocity = ',F5.2,' m/s',/)
C
      WRITE(*,9300)
 9300 FORMAT(' TIME    T  IN    RH IN    T OUT     RH OUT     Q0')
      WRITE(*,9310)
 9310 FORMAT('  H        C        %        C         %        Wh')
     *'            %',/)
C HEAT PROPAGATION LIMIT
      R2P=48.*DIFF*TISC
      R2=SQRT(R2P**2/2.+2.*R2P*DEP/PI)
C
      DO 10 I=1,24
C
C THERMOPHYSICAL PARAMETERS OF THE AIR & HEAT TRANSFER PARAMETERS
C
      CPA=CPAIR(TA(I))
      CA=CAIR(TA(I))
      DA=DAIR(TA(I))
      DVA=DVAIR(TA(I))
C
      REY=RE(FV(L),2.*RAD(J),DA,DVA)
      PRA=PR(CPA,DVA,CA)
      CALL NEWTCO(RAD(J),XL(K),FV(L),DA,CA,REY,PRA,H)
C
      BI=RAD(J)*H/CSO
      SA=2.*H*XL(K)/DA/RAD(J)/CPA/FV(L)
      FO=2.*DIFF*TISC/R2/R2
C
C INTEGRATION
C
      VINT=SIMPN(0.,FO,FO/100.,FINT)
C
      U=EXP(-SA)*(1.+SQRT(BI*SA)*VINT)
C
C AIR OUTLET TEMPERATURE
C
      TO=U*(TA(I)-TG(LM))+TG(LM)
      IF ((TA(I)-TO).LT.2.)THEN
      WRITE(*,9801)I,TA(I)
 9801 FORMAT(1X,I2,6X,F5.2,4X,'SYSTEM NOT PERFORMING')
      GO TO 10
      ENDIF
      TIMER=TIMER+1.
C
      TMO=(TA(I)+TO)/2.
C
      Q0=FV(L)*SUR*DAIR(TMO)*CPAIR(TMO)*(TA(I)-TO)
      Q=Q+Q0
C
C PHSYCHROMETRIC CALCULATIONS
C
      RH2=RH(I)*PWS(TA(I))/PWS(TO)
      WRITE(*,9800)I,TA(I),RH(I),TO,RH2,Q0
 9800 FORMAT(1X,I2,6X,F4.1,5X,F4.1,6X,F4.1,6X,F4.1,6X,F7.1)
 10   CONTINUE
      WRITE(*,9600)Q,TIMER
 9600 FORMAT(' DAILY COOLING LOAD =',F8.1,' Wh',/,
     $' THE SYSTEM IS PERFORMING FOR ',F3.0,' HOURS/DAY',/)
 20   CONTINUE
      CALL FICHR(100,INAM,'OLD',2)
      END
C
C
      SUBROUTINE NEWTCO(RAD,XL,FV,DAIR,CAIR,REY,PRA,H)
C CALCULATION OF THE HEAT TRANSFER COEFFICIENT H
      IF(REY.LT.2000.) THEN
      H=3.66+.0668*2.*RAD*REY*PRA/XL/(1.+.04*(2.*RAD/XL*REY*
     $PRA)**(2./3.))
      ELSE
      H=.023*REY**.8*PRA**.3
      ENDIF
      H=H*CAIR/2./RAD
      RETURN
      END
C
C
      FUNCTION FINT(FO)
C FUNCTION TO BE INTEGRATED
      COMMON BI,SA
      DIMENSION RI(1)
      ARG=2.*SQRT(BI*SA*FO)
      CALL I0(ARG,RIC)
      CALL INUE(ARG,1,RIC,RI)
      FINT=EXP(-BI*FO)*RI(1)*SQRT(FO)
      RETURN
      END
